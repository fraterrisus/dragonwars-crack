plugins {
    id 'java'
}

group 'com.hitchhikerprod'
version '0.9-SNAPSHOT'

repositories {
    mavenCentral()
}

test {}

jar {
    dependsOn compileJava

    destinationDirectory = layout.buildDirectory.dir("libs")
    archiveBaseName = 'dragonwars-crack'

    manifest {
        attributes "Main-Class": 'com.hitchhikerprod.dragonwars.Main'
    }
}

tasks.register('all-jars') {
    group = 'build'
}

var jars = [
        [camel:'Chunk', snake:'raw-printer'],
        [camel:'GameFlag', snake:'game-flag'],
        [camel:'HuffmanDecoder', snake:'huffman-printer'],
        [camel:'MapData', snake:'map-data'],
        [camel:'MetaprogramDecompiler', snake:'decompiler'],
        [camel:'MonsterImageDecoder', snake:'monster'],
]

tasks.register('copy', Copy) {
    from(layout.buildDirectory.dir("libs")) {
        jars.forEach {j -> include "${j.snake}.jar" }
    }
    into '/home/bcordes/pc-games/dragonwars'
}

jars.forEach { jarName ->
    tasks.register(jarName.snake, Jar) {
        group = 'jars'
        dependsOn compileJava

        destinationDirectory = layout.buildDirectory.dir("libs")
        archiveFileName = "${jarName.snake}.jar"

        manifest {
            attributes "Main-Class": "com.hitchhikerprod.dragonwars.${jarName.camel}"
        }

        from sourceSets.main.output
    }

    tasks.named('all-jars') {dependsOn jarName.snake }
    tasks.named('copy') {dependsOn jarName.snake }
}

